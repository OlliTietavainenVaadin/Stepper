//jrebel config
apply plugin: 'rebel'

buildscript {
  repositories {
     mavenCentral()
     // This repo is optional, as since version 1.1.2, we also always deploy to Maven central.
     // Be aware that it could take a bit of time (couple of hours) before a published new
     // version will appear in the central repository.
     mavenRepo(
       name: 'zt-public-snapshots',
       url: 'http://repos.zeroturnaround.com/nexus/content/groups/zt-public/'
     )
  }
  dependencies {
     classpath group: 'org.zeroturnaround', name: 'gradle-jrebel-plugin', version: '1.1.2'
  }
}

//common for both projects
subprojects {
	repositories {
    	mavenCentral()
	}

	apply plugin: "java"

	apply from: "http://plugins.jasoft.fi/vaadin.plugin"
	vaadin.gwt.jvmArgs=["-Xmx1024m"]
	vaadin.jvmArgs=["-Xmx1024m"]
	vaadin.gwt.draftCompile = true
	vaadin.manageWidgetset = false
	vaadin.gwt.strict = true
	vaadin.servletVersion= "3.0"

	sourceCompatibility = "1.6"
	targetCompatibility = "1.6"

	ext.baseNameUpper = project.parent.name.capitalize()
	ext.baseNameLower = project.parent.name.toLowerCase()
	
	group = "org.vaadin.risto.${baseNameLower}"

	//creates archives named "name-version.jar" and "name.war" where name is the name of the main project
	archivesBaseName = baseNameLower
}

//addon project, only creates the Directory-compatible JAR
project(":addon") {
	apply plugin: "eclipse"
	eclipse.project.name = "${baseNameUpper}-addon"

	version = "2.1.2"
	vaadin.widgetset = "${group}.widgetset.${baseNameUpper}Widgetset"

	dependencies {
		testCompile "junit:junit:4.+"
	}

	jar {
		from sourceSets.main.allJava //include sources
	   
		manifest = project.manifest {
			attributes 'Implementation-Title': "${baseNameUpper}"
			attributes 'Implementation-Version': version
			attributes 'Implementation-Vendor': 'Risto Yrjänä / Vaadin'
			attributes 'Vaadin-Package-Version': '1'
			attributes 'Vaadin-License-Title': 'Apache License 2.0'
			attributes 'Vaadin-Widgetsets': vaadin.widgetset
			}
		}
		
	war.enabled = false //war plugin applied by the Vaadin plugin, not needed here
}

//demo project, builds addon and packages demo UI as WAR
project(":demo") {
	apply plugin: "eclipse-wtp"

	eclipse.project.name = "${baseNameUpper}-demo"
	
	dependencies {
		compile project(":addon")
		compile "com.google.guava:guava:15.0"
	}

	war.dependsOn(generateRebel)

	vaadin.widgetset = "${group}.widgetset.${baseNameUpper}DemoWidgetset"
}

task(dist, type: Sync, dependsOn: [':addon:jar', ':demo:war']) {
	description = "Copies the addon JAR and the demo WAR to target/"
	File targetDir = mkdir("target")
	from project(":addon").jar.archivePath
	from project(":demo").war.archivePath
	into targetDir
}